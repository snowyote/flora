<html>
  <head>
    <title>Hi GAF</title>
  </head>
  <body>
    <script src="http://cloud.github.com/downloads/processing-js/processing-js/processing-1.3.6.min.js"></script>
    <script type="text/processing" data-processing-target="mycanvas">

float theta = radians(25f);

void setup() {
  size(640, 360);
  smooth();
}

void draw() {
  background(0);
  frameRate(30);
  stroke(255);
  // Start the tree from the bottom of the screen
  translate(0,height);
    rotate(2*theta);
  // Draw a line 120 pixels
  line(0,0,0,-120);
  // Move to the end of that line
  translate(0,-120);
  // Start the recursive branching!
  branch(120);

}

void forward(float dist) { line(0,0,0,-dist); translate(0, -dist); }
void left() { rotate(-theta); }
void right() { rotate(theta); }

void branch(float l) {
    // going by the plant example of an L-system on Wikipedia:
    // http://en.wikipedia.org/wiki/L-system#Example_8:_Fractal_plant
    //
    // variables : X F
    // constants : + − [ ]
    // start  : X
    // rules  : (X → F-[[X]+X]+F[+FX]-X), (F → FF)

    // halve the length for each iteration: F -> FF
    l *= 0.5;
    // stop when our branches are only a couple pixels
    if (l >= 2) {
        forward(l);
        left();
        pushMatrix();
        pushMatrix();
        branch(l);
        popMatrix();
        right();
        branch(l);
        popMatrix();
        right();
        forward(l);
        pushMatrix();
        right();
        forward(l);
        branch(l);
        popMatrix();
        left();
        branch(l);
    }
}
    </script>
    <canvas id="mycanvas"></canvas>
  </body>
</html>
